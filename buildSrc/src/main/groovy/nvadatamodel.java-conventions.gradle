plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id "com.github.gmazzo.buildconfig"
    id 'nebula.lint'
    id 'jacoco-report-aggregation'
}

group 'com.github.bibsysdev'


version '0.21.0'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

sourceCompatibility = JavaVersion.VERSION_17  // source-code version and must be <= targetCompatibility
targetCompatibility = JavaVersion.VERSION_17 // bytecode target version

test {
    useJUnitPlatform{
        excludeTags "migrationTest"
    }
    failFast = true
    environment "ID_NAMESPACE", "https://www.example.org"
    environment "DATAMODEL_VERSION", "${project.version}"
    finalizedBy(jacocoTestReport)
}

tasks.register('migrationTesting',Test) {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
        includeTags ("migrationTest")
        testLogging {
            events 'skipped', 'passed', 'failed'
        }
    }
    environment "DATAMODEL_VERSION", "${project.version}"
}

migrationTesting.enabled = false

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {

    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.000
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.000
            }
        }
    }
}

jacocoTestReport {
    dependsOn(test)
    reports {
        xml.required
    }
}

pmd {
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
    pmdMain {
        excludes = [
                '**/ResourcesBuildConfig.*'
        ]
    }
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle) .configureEach{
    reports {
        xml.required
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

buildConfig {
    className("ResourcesBuildConfig")   // forces the class name. Defaults to 'BuildConfig'
    packageName(project.group)  // forces the package. Defaults to '${project.group}'
    useJavaOutput() //forces the output to be Java
    buildConfigField("String", "RESOURCES_MODEL_VERSION", "\"${project.version}\"")
}
